/*************************************************************************
 * Copyright (c) 2016-2022, NVIDIA CORPORATION. All rights reserved.
 * Modifications Copyright (c) 2019-2022 Advanced Micro Devices, Inc. All rights reserved.
 * Modifications Copyright (c) Microsoft Corporation. Licensed under the MIT License.
 *
 * See LICENSE.txt for license information
 ************************************************************************/

#define NCCL_LL128_FLAGTHREAD (NCCL_LL128_LINEELEMS - 1)

#ifndef RCCL_USE_WBINVL1_VOL
#if defined(__GFX8__) || defined(__gfx906__) || defined(__gfx908__) || defined(__gfx90a__)
#define RCCL_USE_WBINVL1_VOL 1
#else
#define RCCL_USE_WBINVL1_VOL 0
#endif
#endif

// 128-bit load for FIFO and non-registered user buffers. On gfx1250, sibling
// P2P into a cacheable comm FIFO is not coherent under a nontemporal load; use
// system-scope b128 there. See RCCL_LL_FIFO_SYS_SCOPE in rccl_ptr.h (default
// hipMalloc / cuMem hung; uncached did not). For registered user buffers, use
// load128 which bypasses the cache.
inline __device__ void load128NT(const uint64_t* ptr, uint64_t& v0, uint64_t& v1) {
#if RCCL_LL_FIFO_SYS_SCOPE
  union {
    v4u v;
    uint64_t u64[2];
  } u;
  u.v = __builtin_amdgcn_global_load_b128((v4u_gptr)ptr, RCCL_SYSTEM_SYNCSCOPE);
  v0 = u.u64[0];
  v1 = u.u64[1];
#else
  v0 = __builtin_nontemporal_load((u64_gptr)ptr);
  v1 = __builtin_nontemporal_load((u64_gptr)ptr + 1);
#endif
}

// Plain (cacheable) 128-bit store. Used for non-registered user buffers, and off
// gfx1250 it is also where store128Fifo sends the LL128 comm FIFO. Width is one
// b128 so data and flag stay in a single transaction.
inline __device__ void store128Plain(uint64_t* ptr, uint64_t v0, uint64_t v1) {
  union {
    v4u v;
    uint64_t u64[2];
  } u;
  u.u64[0] = v0;
  u.u64[1] = v1;
  *((v4u_gptr)ptr) = u.v;
}

// LL128 comm-FIFO store. Same 128-bit width as store128Plain (data+flag one
// transaction). On gfx1250 the store is system-scope: a plain store to a
// cacheable FIFO can retire in the writer's cache and never reach a sibling
// partition's poll. See RCCL_LL_FIFO_SYS_SCOPE in rccl_ptr.h.
inline __device__ void store128Fifo(uint64_t* ptr, uint64_t v0, uint64_t v1) {
#if RCCL_LL_FIFO_SYS_SCOPE
  union {
    v4u v;
    uint64_t u64[2];
  } u;
  u.u64[0] = v0;
  u.u64[1] = v1;
  __builtin_amdgcn_global_store_b128((v4u_gptr)ptr, u.v, RCCL_SYSTEM_SYNCSCOPE);
#else
  store128Plain(ptr, v0, v1);
#endif
}

template <typename T, typename RedOp, typename Fan, int Direct, int P2p, bool isNetOffload, int Metadata, int Pipeline,
          int useAcc, int UserRegMode>
class Primitives<T, RedOp, Fan, Direct, ProtoLL128, P2p, isNetOffload, Metadata, Pipeline, useAcc, UserRegMode>
  : public PrimitivesWithoutDirect<
      Primitives<T, RedOp, Fan, Direct, ProtoLL128, P2p, isNetOffload, Metadata, Pipeline, useAcc, UserRegMode>> {
  static constexpr int MaxRecv = Fan::MaxRecv, MaxSend = Fan::MaxSend;
  static constexpr int Input = 0, Output = 1, Acc = 2;
  ;
  RedOp redOp;
  const int tid;
  const int nthreads;
  const int wid;
  const int stepSize;
  const int warp;
  const int warpInBlock; // warp index in thread block
  const bool flagThread;
  const int group;
  const int threadsPerBlock;
  Fan fan;
  T* userBufs[3];
  bool userRegUsed = false; // runtime: user buffers registered (cacheable) -> need cache-bypass
  struct ncclConnInfo* recvConn = NULL;
  volatile uint64_t* recvConnHeadPtr = NULL;
  uint64_t recvConnHead;

  struct ncclConnInfo* sendConn = NULL;
  volatile struct ncclConnFifo* sendConnFifo = NULL;
  volatile uint64_t* sendConnTailPtr = NULL;
  uint64_t sendConnTail;
  volatile uint64_t* sendConnHeadPtr = NULL;
  uint64_t sendConnHead;
  uint64_t sendConnHeadCache; // Cache last seen value

  uint64_t recvStep[MaxRecv];
  uint64_t sendStep[MaxSend];
  uint64_t* recvBuff[MaxRecv];
  uint64_t* sendBuff[MaxSend];

  inline __device__ int recvOffset(int i) {
    return (recvStep[i] % NCCL_STEPS) * stepSize;
  }
  inline __device__ int sendOffset(int i) {
    return (sendStep[i] % NCCL_STEPS) * stepSize;
  }
  inline __device__ uint64_t* recvPtr(int i) {
    return recvBuff[i] + recvOffset(i);
  }
  inline __device__ uint64_t* sendPtr(int i) {
    return sendBuff[i] + sendOffset(i);
  }
  inline __device__ uint64_t recvFlag(int i) {
    return recvStep[i] + 1;
  }
  inline __device__ uint64_t sendFlag(int i) {
    return sendStep[i] + 1;
  }

  uint64_t* barriers;
  uint64_t barrier_next = 0;
  bool skip_fence = false;

  inline __device__ void barrier() {
#if defined(__HIP_PLATFORM_AMD__) || defined(__HIPCC__)
    if (nthreads != WARP_SIZE)
#if defined(__gfx942__) || defined(__gfx950__) || defined(__gfx1250__)
      barrier_generic(__threadfence_block(), nthreads, barrier_next, barriers);
#else
      barrier_generic(__threadfence(), nthreads, barrier_next, barriers);
#endif
#else
    barrier_sync(15 - group, nthreads);
#endif
  }

  int abort = 0;

  __device__ inline int checkAbort(int& abortCache, const int abortValue, int& spins) {
    if (abortCache == 0 && ++spins == NCCL_SPINS_BEFORE_CHECK_ABORT) {
      int abort = __atomic_load_n((ncclShmem.comm.abortFlag), __ATOMIC_SEQ_CST);
      spins = 0;
      if (abort) {
        __atomic_store_n(&ncclShmem.aborted, abort, __ATOMIC_SEQ_CST);
        abortCache |= abortValue;
      }
    }
    return abortCache;
  }

  inline __device__ void waitSend(int nbytes) {
    if (sendConnHeadPtr) {
      int spins = 0;
      while (sendConnHeadCache + NCCL_STEPS < sendConnHead + 1) {
        __builtin_amdgcn_s_sleep(1);
        sendConnHeadCache = ld_relaxed_sys((uint64_t*)sendConnHeadPtr);
        if (checkAbort(abort, 1, spins)) break;
      }
      if (sendConnFifo) {
        st_relaxed_sys(const_cast<ssize_t*>(&sendConnFifo[sendStep[wid] % NCCL_STEPS].size), (ssize_t)nbytes);
      }
      sendConnHead += 1;
    }
  }

  inline __device__ void postRecv() {
    if (recvConnHeadPtr) st_relaxed_sys_global((uint64_t*)recvConnHeadPtr, recvConnHead += 1);
  }
  inline __device__ void postSend() {
    if (sendConnTailPtr) {
      if (skip_fence) {
        __atomic_signal_fence(__ATOMIC_SEQ_CST);
#if defined(__gfx1250__)
        // To be revisited for correctness and performance on gfx1250
        asm volatile("s_wait_loadcnt 0x0\n\ts_wait_storecnt 0x0");
#else
        asm volatile("s_waitcnt lgkmcnt(0) vmcnt(0)");
#endif
        __atomic_signal_fence(__ATOMIC_SEQ_CST);
      } else {
        __threadfence_system();
      }
      st_relaxed_sys_global((uint64_t*)sendConnTailPtr, sendConnTail += 1);
    }
  }

  // User-buffer 128-bit access mode selection.
  // Cache-bypassing (system-scope) loads/stores are only required when this op
  // accesses a registered, potentially-cached user buffer directly. That needs
  // both Direct != 0 (compile-time: this instantiation supports direct access)
  // and registration actually being in use.
  //
  // Whether registration is in use can be resolved at compile time via the
  // UserRegMode template parameter, which lets the caller instantiate clean
  // single-path kernels:
  //   UserRegMode==2 -> never registered: always plain/non-temporal, so the
  //                     system-scope load128/store128 code is never emitted and
  //                     the kernel keeps February-level occupancy.
  //   UserRegMode==1 -> always registered: always system-scope cache-bypass.
  //   UserRegMode==0 -> unknown at compile time: fall back to the per-op
  //                     userRegUsed member (dual path, legacy behavior).
  // When Direct == 0 the bypass folds away entirely regardless of UserRegMode.
  __device__ __forceinline__ bool userBypass() const {
    if (!Direct) {
      return false;
    }
    if (UserRegMode == 1) {
      return true;
    }
    if (UserRegMode == 2) {
      return false;
    }
    return userRegUsed;
  }
  __device__ __forceinline__ void loadUser128(const uint64_t* ptr, uint64_t& v0, uint64_t& v1) {
    if (userBypass()) {
      load128(ptr, v0, v1);
    } else {
      load128NT(ptr, v0, v1);
    }
  }
  __device__ __forceinline__ void storeUser128(uint64_t* ptr, uint64_t v0, uint64_t v1) {
    // Non-registered stores use the plain cacheable path (February behavior) to
    // recover store throughput; only the registered path keeps system-scope.
    if (userBypass()) {
      store128(ptr, v0, v1);
    } else {
      store128Plain(ptr, v0, v1);
    }
  }

  template <int WordPerThread>
  __device__ __forceinline__ void loadRegsBegin(uint64_t (&regs)[WordPerThread], T const* src, int eltN) {
    constexpr int EltPer16B = 16 / sizeof(T);
    // Parametrize the warp-local addressing on the LL128 line geometry.
    // The literals "16" and "4" originally hard-coded 2*LINEELEMS and
    // LINEELEMS/2 for the 64-byte-line case; gfx1250 doubles LINEELEMS
    // (128-byte non-tearing line) so the stride and flag-subgroup width
    // both double accordingly.
    constexpr int LineElems = NCCL_LL128_LINEELEMS;
    constexpr int LineSkip = 2 * WARP_SIZE / LineElems;
    int ix[WordPerThread / 2];
#pragma unroll
    for (int g = 0; g < WordPerThread / 2; g++) {
      ix[g] = g * WARP_SIZE - LineSkip * (g / 2) + wid - (g % 2) * (wid / (LineElems / 2));
    }
    if (reinterpret_cast<uintptr_t>(src) % 16 == 0) {
      /* We are aligned to 16 bytes, so load directly to registers no shmem.
       * Flag threads load half as much data which gets shuffled to the even
       * registers during Finish. The point of splitting into two phases is to
       * defer that shuffle, which incurs a dependency stall, until after other
       * memops are launched by the caller.
       */
#pragma unroll
      for (int g = 0; g < WordPerThread / 2; g++) {
        if (!flagThread || g % 2 == 0) {
          if (ix[g] * EltPer16B < eltN)
            loadUser128((uint64_t*)(src + ix[g] * EltPer16B), regs[2 * g + 0], regs[2 * g + 1]);
        }
      }
    } else {
      // Not aligned. Stage the smallest 16 byte aligned region subsuming the
      // buffer into shmem.
      int misalignment = reinterpret_cast<uintptr_t>(src) % 16;
      uint64_t* src8 = reinterpret_cast<uint64_t*>(reinterpret_cast<uintptr_t>(src) & -uintptr_t(16));
      uint64_t* shm8 = shmemCvtPtr((uint64_t*)ncclScratchForWarp(warpInBlock));
#pragma unroll
      for (int g = 0; g < WordPerThread / 2; g++)
        if ((g * WARP_SIZE + wid) * 16 < misalignment + eltN * sizeof(T))
          loadUser128(src8 + 2 * (g * WARP_SIZE + wid), regs[2 * g + 0], regs[2 * g + 1]);
#pragma unroll
      for (int g = 0; g < WordPerThread / 2; g++)
        storeShmem128(shm8 + 2 * (g * WARP_SIZE + wid), regs[2 * g + 0], regs[2 * g + 1]);

      __syncwarp();

      // Now load from shmem stage to regs. Preserve the same pre-shuffled layout
      // as the aligned case since Finish() will be applied regardless.
      T* shm = (T*)shm8 + misalignment / sizeof(T);
#pragma unroll
      for (int g = 0; g < WordPerThread / 2; g++) {
        // int ix = g*WARP_SIZE - 16*(g/2) + wid - (g%2)*(wid/4);
        if (!flagThread || g % 2 == 0) {
          if (ix[g] * EltPer16B < eltN)
            loadShmemMisaligned128(shm + ix[g] * EltPer16B, regs[2 * g + 0], regs[2 * g + 1]);
        }
      }
    }
  }

  template <int WordPerThread>
  __device__ __forceinline__ void loadRegsFinish(uint64_t (&regs)[WordPerThread]) {
    // Move data out of flag registers into the vacant registers.
#pragma unroll
    for (int g = 1; g < WordPerThread / 2; g += 2) {
      if (flagThread) regs[2 * g] = regs[2 * g - 1];
    }
  }

  template <int WordPerThread>
  __device__ __forceinline__ void storeRegs(T* dst, uint64_t (&regs)[WordPerThread], int eltN) {
    constexpr int EltPer16B = 16 / sizeof(T);
    // Reverse Finish() register permuatation.
#pragma unroll
    for (int g = 1; g < WordPerThread / 2; g += 2) {
      if (flagThread) regs[2 * g - 1] = regs[2 * g];
    }

    // Write to dst if 4-byte aligned, shmem otherwise.
    int misalignment = reinterpret_cast<uintptr_t>(dst) % 16;
    uint64_t* shm8 = shmemCvtPtr((uint64_t*)ncclScratchForWarp(warpInBlock));
    constexpr int LineElems = NCCL_LL128_LINEELEMS;
    constexpr int LineSkip = 2 * WARP_SIZE / LineElems;
#pragma unroll
    for (int g = 0; g < WordPerThread / 2; g++) {
      int ix = g * WARP_SIZE - LineSkip * (g / 2) + wid - (g % 2) * (wid / (LineElems / 2));
      if (!flagThread || g % 2 == 0) {
        if (misalignment == 0 && (ix + 1) * EltPer16B <= eltN) {
          storeUser128((uint64_t*)(dst + ix * EltPer16B), regs[2 * g + 0], regs[2 * g + 1]);
        } else {
          storeShmem128(shm8 + 2 * ix, regs[2 * g + 0], regs[2 * g + 1]);
        }
      }
    }
    __syncwarp();
    // Write rest from shmem to dst. No need to coalesce stores to 16-bytes,
    // the hardware keeps up fine.
    T* shm = (T*)ncclScratchForWarp(warpInBlock);
    int skip = misalignment == 0 ? eltN & -EltPer16B : 0;
    for (int i = skip + wid; i < eltN; i += WARP_SIZE) dst[i] = shm[i];
  }

#define WARP_MASK 0xffffffff

  template <int ELEMS_PER_THREAD, int RECV, int SEND, int SrcBuf, int DstBuf>
  __device__ __forceinline__ void recvReduceSendCopy(uint64_t (&v)[ELEMS_PER_THREAD], int ll128Offset, bool postOp) {
    constexpr int SRC = SrcBuf != -1 ? 1 : 0;
    uint64_t vr[ELEMS_PER_THREAD];

    __syncwarp();
    /************************ Wait first recv ********************/
    if (RECV) {
      uint64_t* ptr = recvPtr(0) + ll128Offset;
      uint64_t flag = recvFlag(0);
      bool needReload;
      int spins = 0;
      do {
        needReload = false;
#pragma unroll
        for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
          load128NT(ptr + u * WARP_SIZE, vr[u], vr[u + 1]);
          needReload |= flagThread && (vr[u + 1] != flag);
        }
        needReload &= (0 == checkAbort(abort, 1, spins));
      } while (__any(needReload));
#pragma unroll
      for (int u = 0; u < ELEMS_PER_THREAD; u += 2) load128NT(ptr + u * WARP_SIZE, vr[u], vr[u + 1]);
    }

    /************* Finish register load **************/
    if (SRC) {
      // By deferring register shuffle here we've overlapped spinning on first
      // peer's data with memory loads of src data.
      loadRegsFinish(v);
      if (SrcBuf == Input) {
#pragma unroll
        for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
          v[u] = applyPreOp(redOp, v[u]);
          if (!flagThread) v[u + 1] = applyPreOp(redOp, v[u + 1]);
        }
      }
    }

    /************************ Recv rest *********************/
    if (RECV) {
      { // Consume data from first recv
#pragma unroll
        for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
          v[u] = SRC ? applyReduce(redOp, vr[u], v[u]) : vr[u];
          v[u + 1] = SRC ? applyReduce(redOp, vr[u + 1], v[u + 1]) : vr[u + 1];
        }
      }

      // Yes, for some template arguments this code will be unreachable.  That's fine.
      // coverity[dead_error_line]
      for (int i = 1; i < MaxRecv && i < fan.nrecv(); i++) {
        uint64_t flag = recvFlag(i);
        uint64_t* ptr = recvPtr(i) + ll128Offset;
        bool needReload;
        int spins = 0;
        do {
          needReload = false;
#pragma unroll
          for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
            load128NT(ptr + u * WARP_SIZE, vr[u], vr[u + 1]);
            needReload |= flagThread && (vr[u + 1] != flag);
          }
          needReload &= (0 == checkAbort(abort, 1, spins));
        } while (__any(needReload));

#pragma unroll
        for (int u = 0; u < ELEMS_PER_THREAD; u += 2) load128NT(ptr + u * WARP_SIZE, vr[u], vr[u + 1]);

#pragma unroll
        for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
          v[u] = applyReduce(redOp, vr[u], v[u]);
          v[u + 1] = applyReduce(redOp, vr[u + 1], v[u + 1]);
        }
      }
    }
    /********************** End Recv ************************/

    if (postOp) {
#pragma unroll
      for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
        v[u] = applyPostOp(redOp, v[u]);
        v[u + 1] = applyPostOp(redOp, v[u + 1]);
      }
    }

#if RCCL_USE_WBINVL1_VOL
    if (tid == 0) __builtin_amdgcn_buffer_wbinvl1();
#endif
    /************************ Send **************************/
    if (SEND) {
      // Yes, for some template arguments this code will be unreachable.  That's fine.
      // coverity[dead_error_line]
      for (int i = 1; i < MaxSend && i < fan.nsend(); i++) {
        uint64_t flag = sendFlag(i);
        uint64_t* ptr = sendPtr(i) + ll128Offset;
#pragma unroll
        for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
          store128Fifo(ptr + u * WARP_SIZE, v[u], flagThread ? flag : v[u + 1]);
        }
      }
      uint64_t flag = sendFlag(0);
      uint64_t* ptr = sendPtr(0) + ll128Offset;
#pragma unroll
      for (int u = 0; u < ELEMS_PER_THREAD; u += 2) {
        store128Fifo(ptr + u * WARP_SIZE, v[u], flagThread ? flag : v[u + 1]);
      }
    }
    /********************** End Send ************************/
  }

  static constexpr int WireWordPerSlice = WARP_SIZE * NCCL_LL128_SHMEM_ELEMS_PER_THREAD;
  static constexpr int DataEltPerSlice =
    (WireWordPerSlice - WireWordPerSlice / NCCL_LL128_LINEELEMS) * (sizeof(uint64_t) / sizeof(T));

  template <int RECV, int SEND, int SrcBuf, int DstBuf>
  __device__ __forceinline__ void GenericOp(intptr_t srcIx, intptr_t dstIx, int nelem, bool postOp) {
    constexpr int SRC = SrcBuf != -1 ? 1 : 0;
    constexpr int DST = DstBuf != -1 ? 1 : 0;
    T const* srcPtr = SrcBuf == -1 ? nullptr : userBufs[SrcBuf] + srcIx;
    T* dstPtr = DstBuf == -1 ? nullptr : userBufs[DstBuf] + dstIx;
    T* accPtr = (DstBuf == -1 || !useAcc) ? nullptr : userBufs[Acc] + dstIx;
    int wireOffset = WireWordPerSlice * warp + 2 * wid;
    const int nwarps = nthreads / WARP_SIZE;
    nelem = nelem < 0 ? 0 : nelem;

    if (SEND) waitSend(divUp(nelem, DataEltPerSlice) * WireWordPerSlice * sizeof(uint64_t));
    barrier();

    sqtt_marker_enter("PRIM_LL128_DATA_PROCESS");
    nelem -= DataEltPerSlice * warp;
    srcPtr += DataEltPerSlice * warp;
    dstPtr += DataEltPerSlice * warp;
    if (accPtr != nullptr) accPtr += DataEltPerSlice * warp;
    while (nelem > 0) {
      const int eltInSlice = min(nelem, DataEltPerSlice);
      uint64_t regs[NCCL_LL128_SHMEM_ELEMS_PER_THREAD];
      if (SRC) loadRegsBegin(regs, srcPtr, eltInSlice);
      recvReduceSendCopy<NCCL_LL128_SHMEM_ELEMS_PER_THREAD, RECV, SEND, SrcBuf, DstBuf>(regs, wireOffset, postOp);
      if (DST) {
        if (accPtr != nullptr) {
          uint64_t accRegs[NCCL_LL128_SHMEM_ELEMS_PER_THREAD];
          loadRegsBegin(accRegs, accPtr, eltInSlice);
          loadRegsFinish(accRegs);
          accPtr += DataEltPerSlice * nwarps;
#pragma unroll
          for (int u = 0; u < NCCL_LL128_SHMEM_ELEMS_PER_THREAD; u++) {
            regs[u] = applyReduce(redOp, accRegs[u], regs[u]);
          }
        }
        storeRegs(dstPtr, regs, eltInSlice);
      }

      wireOffset += WireWordPerSlice * nwarps;
      srcPtr += DataEltPerSlice * nwarps;
      dstPtr += DataEltPerSlice * nwarps;
      nelem -= DataEltPerSlice * nwarps;
    }

    barrier();

    sqtt_marker_exit("PRIM_LL128_DATA_PROCESS");

    if (SEND)
      for (int i = 0; i < MaxSend; i++) sendStep[i] += 1;
    if (SEND) postSend();
    if (RECV)
      for (int i = 0; i < MaxRecv; i++) recvStep[i] += 1;
    if (RECV) postRecv();
  }

  __device__ __forceinline__ void loadRecvConn(struct ncclConnInfo* conn, int i) {
    recvBuff[i] = (uint64_t*)conn->buffs[NCCL_PROTO_LL128];
    recvStep[i] = conn->step;
    if (wid == i) recvConn = conn;
  }
  __device__ __forceinline__ void loadRecvSync() {
    if (tid >= nthreads - WARP_SIZE && wid < fan.nrecv()) {
      recvConnHeadPtr = recvConn->head;
      recvConnHead = recvConn->step;
    }
  }

  __device__ __forceinline__ void loadSendConn(struct ncclConnInfo* conn, int i) {
    sendBuff[i] = (uint64_t*)conn->buffs[NCCL_PROTO_LL128];
    sendStep[i] = conn->step;
    if (wid == i) sendConn = conn;
  }
  __device__ __forceinline__ void loadSendSync() {
    if (tid < fan.nsend()) {
      sendConnHeadPtr = sendConn->head;
      sendConnHeadCache = *sendConnHeadPtr;
      sendConnHead = sendConn->step;
      sendConnFifo = sendConn->connFifo;
    }
    if (tid >= nthreads - WARP_SIZE && wid < fan.nsend()) {
      if (sendConn->connFifo) {
        sendConnTailPtr = sendConn->tail;
        sendConnTail = sendConn->step;
      }
    }
  }

public:
  __device__ Primitives(const int tid, const int nthreads, int const* recvPeers, int const* sendPeers,
                        void const* inputBuf, void* outputBuf, uint64_t redOpArg, uint8_t group = 0,
                        uint8_t connIndexRecv = 0, uint8_t connIndexSend = 0, struct ncclDevWorkColl* e = nullptr,
                        bool ipcReg = false, bool netReg = false, int stepSize_ = 0)
    : redOp(redOpArg), tid(tid), nthreads(nthreads), wid(tid % WARP_SIZE), /*compiler warnings*/
      stepSize(ncclShmem.comm.buffSizes[NCCL_PROTO_LL128] / NCCL_STEPS / sizeof(uint64_t)), warp(tid / WARP_SIZE),
      warpInBlock(threadIdx.x / WARP_SIZE),
      flagThread((tid % (NCCL_LL128_LINEELEMS / 2)) == (NCCL_LL128_LINEELEMS / 2 - 1)), group(group),
      threadsPerBlock(blockDim.x) {
#ifdef ENABLE_WARP_SPEED
    auto* channel = ncclShmem.warpComm ? &ncclShmem.warpChannel[warpInBlock] : &ncclShmem.channel;
#else
    auto* channel = &ncclShmem.channel;
#endif
    barriers = &ncclShmem.groups[group].barrier;
    int nrecv = 0, nsend = 0;
    while (nrecv < MaxRecv && recvPeers[nrecv] >= 0) {
      loadRecvConn(&channel->peers[recvPeers[nrecv]]->recv[connIndexRecv], nrecv);
      nrecv++;
    }
    while (nsend < MaxSend && sendPeers[nsend] >= 0) {
      loadSendConn(&channel->peers[sendPeers[nsend]]->send[connIndexSend], nsend);
      nsend++;
    }
    this->fan = Fan(nrecv, nsend);
    // Coverity reports recvConn and sendConn being possibly NULL at this point but that won't actually
    // happen given the two "while" loops just above.
    // coverity[var_deref_model:FALSE]
    loadRecvSync();
    // coverity[var_deref_model:FALSE]
    loadSendSync();
    userRegUsed = (e != nullptr) && (e->regUsed || e->netRegUsed);
    setDataPtrs(inputBuf, outputBuf, e != nullptr ? e->acc : nullptr);
#if RCCL_HAVE_GLOBAL_DWORDX4_BUILTINS
    skip_fence = !ncclShmem.comm.cheapPostSendFenceOff;
#else
    // The cheap post-peer fence is only safe with global DWORDX4 builtins
    // (system-scope cache-bypassing stores); otherwise always use the full fence.
    skip_fence = false;
#endif
  }

  __forceinline__ __device__ Primitives(int tid, int nthreads, int const* recvPeers, int const* sendPeers,
                                        void const* inputBuf, void* outputBuf, uint64_t redOpArg, uint8_t group,
                                        uint8_t connIndexRecv, uint8_t connIndexSend, struct ncclDevWorkColl* collWork,
                                        struct ncclDevWorkP2p* p2pWork, int stepSize_ = 0, int mode = primsModeDefault)
    : Primitives(tid, nthreads, recvPeers, sendPeers, inputBuf, outputBuf, redOpArg, group, connIndexRecv,
                 connIndexSend, collWork) {}

  __device__ ~Primitives() {
    // Save steps for the next operation
    if (tid >= nthreads - WARP_SIZE && wid < fan.nrecv()) recvConn->step = recvConnHead;
    if (tid < fan.nsend()) sendConn->step = sendConnHead;
    // Ensure all steps written back
    barrier();
  }

  __device__ void setDataPtrs(void const* inputBuf, void* outputBuf, void const* acc = nullptr) {
    userBufs[Input] = (T*)inputBuf;
    userBufs[Output] = (T*)outputBuf;
    userBufs[Acc] = (T*)acc;
  }

  __device__ void moveDataPtrs(intptr_t delta) {
    userBufs[Input] += delta;
    userBufs[Output] += delta;
  }

  __device__ void send(intptr_t inpIx, int eltN) {
    GenericOp<0, 1, Input, -1>(inpIx, -1, eltN, false);
  }
  __device__ void sendFromOutput(intptr_t outIx, int eltN) {
    GenericOp<0, 1, Output, -1>(outIx, -1, eltN, false);
  }
  __device__ void recv(intptr_t outIx, int eltN, bool postOp = false) {
    GenericOp<1, 0, -1, Output>(-1, outIx, eltN, postOp);
  }
  __device__ void recvReduceSend(intptr_t inpIx, int eltN) {
    GenericOp<1, 1, Input, -1>(inpIx, -1, eltN, false);
  }
  __device__ void recvReduceCopy(intptr_t inpIx, intptr_t outIx, int eltN, bool postOp = false) {
    GenericOp<1, 0, Input, Output>(inpIx, outIx, eltN, postOp);
  }
  __device__ void copySend(intptr_t inpIx, intptr_t outIx, int eltN, bool postOp = false) {
    GenericOp<0, 1, Input, Output>(inpIx, outIx, eltN, postOp);
  }
  __device__ void recvCopySend(intptr_t outIx, int eltN, bool postOp = false) {
    GenericOp<1, 1, -1, Output>(-1, outIx, eltN, postOp);
  }
  __device__ void recvReduceCopySend(intptr_t inpIx, intptr_t outIx, int eltN, bool postOp = false) {
    GenericOp<1, 1, Input, Output>(inpIx, outIx, eltN, postOp);
  }
  __device__ void recvSend(int eltN) {
    return GenericOp<1, 1, -1, -1>(-1, -1, eltN, false);
  }
};
